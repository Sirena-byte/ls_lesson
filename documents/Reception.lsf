MODULE Reception;

REQUIRE Documents, AppMenu, Organization;
NAMESPACE Documents;

CLASS Reception 'Приходы' : Document;

name 'Наименование' = DATA STRING[50] (Reception) NONULL;
price 'Цена' = DATA NUMERIC[10,2] (Reception) NONULL;

//композиция
suplier = DATA Organization(Reception);
organizationName 'Поставщик' (Reception o) = name(suplier(o));

// контроль данных
unique = GROUP AGGR Reception o BY number(o), date(o), name(o);
CONSTRAINT SETCHANGED(suplier(Reception o)) AND NOT organizationType(suplier(o)) = OrganizationType.supplier
    CHECKED BY suplier[Reception] MESSAGE 'Для приходной накладной организация должна быть поставщиком';

FORM editReception 'Приход'
    OBJECTS r = Reception PANEL // опция panel - только одна запись
    PROPERTIES (r) number, date, organizationName, name, quantity, price
    EDIT Reception OBJECT r
;

DESIGN editReception {
    OBJECTS {
        NEW cntDoc {
            horizontal = TRUE; // распределяем элементы слева-направо-сверху-вниз
            caption = 'Накладная';
            height = 60;
            MOVE PROPERTY (number(r));
            MOVE PROPERTY (date(r));
        }
        NEW cntOrg{
            horizontal = TRUE ;
            caption = 'Поставщик';
            height = 60;
            MOVE PROPERTY (organizationName(r)) {caption = 'Фирма';}
        }
        NEW cntRow {
            horizontal = FALSE ;
            caption = 'Строки накладной';
            MOVE PROPERTY (name(r));
            MOVE PROPERTY (quantity(r));
            MOVE PROPERTY (price(r));
        }
    }
}

FORM viewReception 'Приходы'
    OBJECTS r = Reception // опция по умолчанию GRID    
    PROPERTIES (r) READONLY number, date, organizationName, name, quantity, price, userName, userDate
    PROPERTIES (r) NEWSESSION NEW, EDIT, DELETE
;


NAVIGATOR {
    documents {
        NEW FORM viewReception;
    }
}



